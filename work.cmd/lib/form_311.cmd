::<<<<<<<<<<<<<<<<F<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<
rem =========================================
rem Обработка файлов по форме 311
rem =========================================

rem Задание переменных
call :setVariable
rem Забираем файлы с монитора
call :from_Monitor
rem Переносим [A,B]N10123??????????.arj на вход монитора для отправки в ЦБ
call :to_Monitor
rem Отправка файлов S[F,B]C??3510123_????????????_24900000??????????_???.xml из Диасофта на подписание
call :move_from_Diasoft_to_sign
rem Отправка файлов S[F,B]C??3510123_????????????_24900000??????????_???.xml с подписи на шифрование
call :move_from_sign_to_encrypt
rem Отправка файлов S[F,B]C??3510123_????????????_24900000??????????_???.xml с шифрования на архивирование
call :move_from_encrypt_to_arj
rem Отправка архивов [A,B]N10123??????????.arj на подписание
call :move_arj_to_sign
rem Отправка архивов [A,B]N10123??????????.arj в ЦБ
call :send_arj_to_CB

rem Отправка UV[A,B]N10123??????????.XML файлов в bck
call :move_UV_to_BCK
rem Отправка S[F,B][E,F,P,R]??3510123_????????????_24900000??????????_???.xml файлов на проверку подписи
call :move_answer_to_verify
rem Отправка S[F,B][E,F,P,R]??3510123_????????????_24900000??????????_???.xml файлов на отправку в Diasoft
call :move_answer_311_to_Temp

exit /b 0

::>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>






























:setVariable
rem ======================================================================
echo  ** Задание переменных
rem ======================================================================
rem set common variables
call c:\work.cmd\lib\lib.cmd "setVariable"
exit /b 0


:from_Monitor
rem ======================================================================
echo  ** Забираем файлы с монитора
rem ======================================================================
setlocal
set from=t:\fromCB\out\
set to=c:\work\cb\in\
rem set to55=s:\in\
set mask=UVAN10123??????????.xml#UVBN10123??????????.xml#ON10123??????????.ARJ#S10123????????.arj#NN10123??????????.ARJ
where /r %from% /q %mask:#= % > nul
if not errorlevel 1 (call %dirLib%lib.cmd "moveFile %from% %to% %mask% toSystem")
rem if not errorlevel 1 (call %dirLib%lib.cmd "copyAndMoveFile %from% %to55% %to% %mask% toARMCB toSystem")
endlocal&exit /b 0


:to_Monitor
rem ======================================================================
echo  ** Переносим [A,B]N10123??????????.arj на вход монитора для отправки в ЦБ
rem ======================================================================
setlocal
set from=c:\work\cb\out\
set to=t:\toCB\in\
set mask=AN10123??????????.arj#BN10123??????????.arj
where /r %from% /q %mask:#= % > nul
if not errorlevel 1 (call %dirLib%lib.cmd "moveFile %from% %to% %mask% toMonitor")
endlocal&exit /b 0


:move_from_Diasoft_to_sign
rem ======================================================================
echo  ** Отправка файлов S[F,B]C??3510123_*.xml из Диасофта на подписание
rem ======================================================================
setlocal
set from=c:\work\diasoft_check\ok\
set   to=c:\work\processing\sign_sign\in\
set mask=SFC??3510123_????????????_24900000??????????_???.xml#SBC??3510123_????????????_24900000??????????_???.xml
where /r %from% /q %mask:#= % > nul
if not errorlevel 1 (call %dirLib%lib.cmd "copyAndMoveFile %from% %dirArhLong% %to% %mask% toBck toSign")
rem if not errorlevel 1 (call %dirLib%lib.cmd "copyAndMoveFile %from% %dirArhLong% %to% %mask% toBck toSign CreateList %dirArh%%timeSm%.list")
endlocal&exit /b 0


:move_from_sign_to_encrypt
rem ======================================================================
echo  ** Отправка файлов S[F,B]C??3510123_*.xml с подписи на шифрование
rem ======================================================================
setlocal
set from=c:\work\processing\sign_sign\out\
set   to=c:\work\processing\encrypt_encrypt\in\
set mask=SFC??3510123_????????????_24900000??????????_???.xml#SBC??3510123_????????????_24900000??????????_???.xml
where /r %from% /q %mask:#= % > nul
if not errorlevel 1 (call %dirLib%lib.cmd "moveFile %from% %to% %mask% toEncrypt")
endlocal&exit /b 0


:move_from_encrypt_to_arj
rem ======================================================================
echo  ** Отправка файлов S[F,B]C??3510123_*.xml с шифрования на архивирование
rem ======================================================================
setlocal
set from=c:\work\processing\encrypt_encrypt\out\
set   to=c:\work\to_arj\in\
set mask=SFC??3510123_????????????_24900000??????????_???.xml#SBC??3510123_????????????_24900000??????????_???.xml
where /r %from% /q %mask:#= % > nul
if not errorlevel 1 (call %dirLib%lib.cmd "moveFile %from% %to% %mask% toArchiveDir")
endlocal&exit /b 0


:move_arj_to_sign
rem ======================================================================
echo  ** Отправка архивов [A,B]N10123??????????.arj на подписание
rem ======================================================================
setlocal
set from=c:\work\diasoft_check\ok\
set   to=c:\work\processing\sign_sign\in\
set mask=AN10123??????????.arj#BN10123??????????.arj
where /r %from% /q %mask:#= % > nul
if errorlevel 1 (endlocal&exit /b 0)
set listFiles=%dirLogs%%timeSm%.files
call %dirLib%lib.cmd "getBatchFolder homeFolder"
cd /d %from%
dir /b /ON %mask:#= % > %listFiles%
timeout 3
for /f "tokens=*" %%I in (%listFiles%) do (
  call %dirLib%lib.cmd "setTime"
  call %dirLib%lib.cmd "saveListArh %from%%%I %dirLogs%%%I.%timeSn%.list"
  call %dirLib%lib.cmd "moveDouble %from% %to% %%I"
  call %dirLib%lib.cmd "writeLog %%I toSign %from% %to%"
)
if not "%homeFolder:~0,2%"=="\\" (cd /d %homeFolder%)
del %listFiles%
endlocal&exit /b 0


:send_arj_to_CB
rem ======================================================================
echo  ** Отправка архивов [A,B]N10123??????????.arj в ЦБ
rem ======================================================================
setlocal
set from=c:\work\processing\sign_sign\out\
set   to=c:\work\cb\out\
set mask=AN10123??????????.arj#BN10123??????????.arj
where /r %from% /q %mask:#= % > nul
if not errorlevel 1 (call %dirLib%lib.cmd "moveFile %from% %to% %mask% toCB")
endlocal&exit /b 0


:move_UV_to_BCK
rem ======================================================================
echo  ** Отправка UV[A,B]N10123??????????.XML файлов в bck
rem ======================================================================
setlocal
set from=c:\work\cb_check\ok\
set mask=UVAN10123??????????.xml#UVBN10123??????????.xml
where /r %from% /q %mask:#= % > nul
if not errorlevel 1 (call %dirLib%lib.cmd "moveFile %from% %dirArhLong% %mask% toBck")
rem if not errorlevel 1 (call %dirLib%lib.cmd "moveFile %from% %dirArhLong% %mask% toBck CreateList %dirArh%%timeSm%.list")
endlocal&exit /b 0


:move_answer_to_verify
rem ======================================================================
echo  ** Отправка S[F,B][E,F,P,R]??3510123_*.xml файлов на проверку подписи
rem ======================================================================
setlocal
set from=c:\work\cb_check\ok\
set   to=c:\work\processing\sign_verify\in\
set mask=SFE??3510123_????????????_24900000??????????_???.xml#SFF??3510123_????????????_24900000??????????_???.xml#SFP??3510123_????????????_24900000??????????_???.xml#SFR??3510123_????????????_24900000??????????_???.xml#SBE??3510123_????????????_24900000??????????_???.xml#SBF??3510123_????????????_24900000??????????_???.xml#SBP??3510123_????????????_24900000??????????_???.xml#SBR??3510123_????????????_24900000??????????_???.xml
where /r %from% /q %mask:#= % > nul
if not errorlevel 1 (call %dirLib%lib.cmd "moveFile %from% %to% %mask% toVerify")
endlocal&exit /b 0


:move_answer_311_to_Temp
rem ======================================================================
echo  ** Отправка S[F,B][E,F,P,R]??3510123_????????????_24900000??????????_???.xml файлов на отправку в Diasoft
rem ======================================================================
setlocal
set from=c:\work\processing\sign_verify\out\
rem set   to=N:\exchange\311p\FilGo\receive\
set   to=c:\work\diasoft\in\
set mask=SFE??3510123_????????????_24900000??????????_???.xml#SFF??3510123_????????????_24900000??????????_???.xml#SFP??3510123_????????????_24900000??????????_???.xml#SFR??3510123_????????????_24900000??????????_???.xml#SBE??3510123_????????????_24900000??????????_???.xml#SBF??3510123_????????????_24900000??????????_???.xml#SBP??3510123_????????????_24900000??????????_???.xml#SBR??3510123_????????????_24900000??????????_???.xml
where /r %from% /q %mask:#= % > nul
if not errorlevel 1 (call %dirLib%lib.cmd "copyAndMoveFile %from% %dirArhLong% %to% %mask% toBck toDiasoft")
rem if not errorlevel 1 (call %dirLib%lib.cmd "copyAndMoveFile %from% %dirArhLong% %to% %mask% toBck toDiasoft CreateList %dirArh%%timeSm%.list")
endlocal&exit /b 0




